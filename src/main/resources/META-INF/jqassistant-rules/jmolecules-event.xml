<jqa:jqassistant-rules xmlns:jqa="http://www.buschmais.com/jqassistant/core/rule/schema/v1.4">

    <group id="jmolecules-event:Default">
        <includeConcept refId="jmolecules-event:DomainEventType" severity="info"/>
        <includeConcept refId="jmolecules-event:DomainEventExtend" severity="info"/>
    </group>

    <concept id="jmolecules-event:DomainEventType">
        <description>Labels all Java types which are annotated by
            org.jmolecules.event.annotation.DomainEvent as :JMolecules:Event:DomainEvent.</description>
        <cypher><![CDATA[
            MATCH
                (p:Package)-[:CONTAINS]->(e:Type)-[:ANNOTATED_BY]->(a)-[:OF_TYPE]->(annotation:Type)
            WHERE
                annotation.fqn = "org.jmolecules.event.annotation.DomainEvent"
            SET
                e:JMolecules:Event:DomainEvent
            WITH
                p, e, a
            OPTIONAL MATCH
                (a)-[:HAS]->(namespace:Value{name: "namespace"})
            SET
                e.eventNamespace = coalesce(namespace.value, p.fqn)
            WITH
                e, a
            OPTIONAL MATCH
                (a)-[:HAS]->(name:Value{name: "name"})
            SET
                e.eventName = coalesce(name.value, e.name)
            RETURN
                e AS DomainEvent
        ]]></cypher>
    </concept>

    <concept id="jmolecules-event:DomainEventExtend">
        <description>Labels all Java types which extend from
            org.jmolecules.event.types.DomainEvent as :JMolecules:Event:DomainEvent.</description>
        <cypher><![CDATA[
            MATCH
              (p:Package)-[:CONTAINS]->(domainEvent:Type)-[:EXTENDS|IMPLEMENTS*]->(event:Type)
            WHERE
              event.fqn = "org.jmolecules.event.types.DomainEvent"
            SET
              domainEvent:JMolecules:Event:DomainEvent,
              domainEvent.eventNamespace = p.fqn,
              domainEvent.eventName = domainEvent.name
            RETURN
              domainEvent AS DomainEvent
        ]]></cypher>
    </concept>

</jqa:jqassistant-rules>
